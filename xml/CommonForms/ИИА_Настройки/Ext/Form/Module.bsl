
&НаКлиенте
Процедура Зарегистрироваться(Команда)
	
	ОткрытьФормуРегистрации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРегистрации()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияФормыРегистрации", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ИИА_РегистрацияGitsell", , ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыРегистрации(Результат, Параметры) Экспорт
	
	// После регистрации обновляем данные формы с сервера, так как токен сохранился в базе
	ОбновитьДанныеССервера();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеССервера()
	
	Настройки = ИИА_Сервер.ПолучитьНастройкиПользователя();
	Provider_ApiKey = Настройки.Provider_ApiKey;
	Provider_BaseUrl = Настройки.Provider_BaseUrl;
	email = Настройки.email;
	Модель = Настройки.Модель;
	ЛимитТокеновНаДиалог = Настройки.ЛимитТокеновНаДиалог;
	
	ЗаполнитьСписокМоделей();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокМоделей()
	
	СписокМоделей = Элементы.Модель.СписокВыбора;
	СписокМоделей.Очистить();
	
	Модели = ИИА_GitsellСервер.Gitsell_СписокМоделей();
	Для Каждого МодельИИ Из Модели Цикл
		СписокМоделей.Добавить(МодельИИ.id, МодельИИ.name);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьСервер();
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура СохранитьСервер()
	
	Пользователь = ИИА_Сервер.ИмяТекущегоПользователя();
	
	НаборЗаписей = РегистрыСведений.ИИА_НастройкиПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	НаборЗаписей.Прочитать();
	
	Если НаборЗаписей.Количество() = 0 Тогда
		Запись = НаборЗаписей.Добавить();
		Запись.Пользователь = Пользователь;
	Иначе
		Запись = НаборЗаписей[0];
	КонецЕсли;
	
	Запись.Provider_ApiKey = Provider_ApiKey;
	Запись.Provider_BaseUrl = Provider_BaseUrl;
	Запись.email = email;
	Запись.Модель = Модель;
	Запись.ЛимитТокеновНаДиалог = ЛимитТокеновНаДиалог;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	
	ОбновитьДанныеССервера();
	
	Если НЕ ЗначениеЗаполнено(Модель) Тогда 
		Модель = "auto";    
	КонецЕсли;
	
	Попытка
		РезультатОбновления = ИИА_GitsellСервер.GitHub_ПроверитьОбновление();
		Если РезультатОбновления.Доступно Тогда
			ЕстьОбновление = Истина;
			НоваяВерсия = РезультатОбновления.НоваяВерсия;
			СсылкаНаСкачивание = РезультатОбновления.СсылкаНаСкачивание;
			Элементы.ГруппаОбновление.Видимость = Истина;
		КонецЕсли;
	Исключение
		// Тихая обработка
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРасширение(Команда)
	
	ОчиститьСообщения();
	Статус = ОбновитьРасширениеНаСервере();
	
	Если Статус.Успех Тогда
		ПоказатьОповещениеПользователя("Обновление выполнено", , "Расширение обновлено до версии " + НоваяВерсия + ". Перезапустите сеанс 1С.");
		Элементы.ГруппаОбновление.Видимость = Ложь;
	Иначе
		Сообщить(Статус.Ошибка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкачатьРасширение(Команда)
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаСкачивание) Тогда
		ПоказатьОповещениеПользователя("Скачать невозможно", , "Ссылка на скачивание не определена.");
		Возврат;
	КонецЕсли;
	
	ПерейтиПоНавигационнойСсылке(СсылкаНаСкачивание);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьРасширениеНаСервере()
	
	Результат = Новый Структура("Успех, Ошибка", Ложь, "");
	
	Попытка
		ДвоичныеДанные = ИИА_GitsellСервер.GitHub_СкачатьФайл(СсылкаНаСкачивание);
		ИИА_GitsellСервер.УстановитьРасширение(ДвоичныеДанные);
		Результат.Успех = Истина;
	Исключение
		Результат.Ошибка = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции
